##################################################################################
# PROVIDERS
##################################################################################
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.16"
    }
    teleport = {
      source  = "terraform.releases.teleport.dev/gravitational/teleport"
      version = "14.0.0"
    }
  }
  required_version = ">= 1.2.0"
}
provider "aws" {
  region = var.aws_region
}
provider "teleport" {
  # Update addr to point to your Teleport tenant URL's host:port
  addr = "${var.proxy_service_address}:443"
  # An identity file is used to authenticate against the Terraform user impersonation and role of the Teleport cluster
  identity_file_path = "../../terraform-identity"
}

provider "random" {
}

locals {
  bot_name = "ansible"
}

resource "random_string" "bot_token" {
  length = 32
  special = true
  override_special = "-.+"
}

#resource "time_offset" "bot_ansible_token_expiry" {
#  offset_hours = 1
#}

resource "teleport_provision_token" "bot_ansible" {
  metadata = {
    #expires     = time_offset.bot_ansible_token_expiry.rfc3339
    expires = timeadd(timestamp(), "1h")
    description = "Bot join token for ${local.bot_name} generated by Terraform"
    name        = random_string.bot_token.result
  }
  spec = {
    roles       = ["Bot"]
    bot_name    = local.bot_name
    join_method = "token"
  }
}

resource "teleport_bot" "ansible" {
  name     = local.bot_name
  token_id = teleport_provision_token.bot_ansible.metadata.name
  roles    = [teleport_role.ansible.id]
}

resource "teleport_role" "ansible" {
  metadata = {
    name        = "ansible-role"
    description = "Role to allow Ansible access. This should be adjusted to your service of choice"
  }
  spec = {
    allow = {
      logins = ["root"] # Replace this with the system user Ansible will use
      node_labels = {
        "*" : ["*"] # Adjust node labels to match the nodes Ansible needs to access
      }
    }
  }
}

resource "random_string" "main_token" {
  length = 32
}

resource "teleport_provision_token" "main" {
  spec = {
    roles = [
      "Node",
    ]
    name = random_string.main_token.result
  }
  metadata = {
    expires = timeadd(timestamp(), "1h")
  }
}

data "aws_ami" "ubuntu" {
  most_recent = true
  filter {
    name   = "name"
    values = ["ubuntu/images/hvm-ssd/ubuntu-*-22.04-amd64-server-*"]
  }
  filter {
    name   = "virtualization-type"
    values = ["hvm"]
  }
  owners = ["099720109477"] # aws ec2 describe-images --image-ids ami-024e6efaf93d85776 --output json | jq '.Images[] | {Platform, OwnerId}'
}

resource "aws_instance" "main" {
  ami           = data.aws_ami.ubuntu.id
  instance_type = "t3.micro"
  user_data = templatefile("./config/userdata", {
    token  = teleport_provision_token.main.metadata.name
    bot_token = teleport_bot.ansible.token_id
    domain = var.proxy_service_address
    major  = " v14"
  })

  // The following two blocks adhere to security best practices.
  metadata_options {
    http_endpoint = "enabled"
    http_tokens   = "required"
  }

  root_block_device {
    encrypted = true
  }
  tags = {
    Name = "dlg-ansible"
  }
}